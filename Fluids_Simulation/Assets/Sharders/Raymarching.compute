#pragma kernel CSMain

Texture2D<float4> Source;
RWTexture2D<float4> Destination;
Texture2D<float4> _DepthTexture;

float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;

static const float maxDst = 80;
static const float epsilon = 0.001f;
static const float shadowBias = epsilon * 50;

struct Sphere
{
    float pressure;
    float density;
    float3 force;
    float3 velocity;
    float3 position;
};

StructuredBuffer<Sphere> spheres;
int numParticles;
float sphereRadius;
float blendStrength;
float3 waterColor;
float3 _Light;
float3 _AmbientLight;
float3 _CameraPos;

struct Ray {
    float3 origin;
    float3 direction;
};

// This function calculates the distance from the camera/eye to the surface of a sphere.
float SphereDistance(float3 eye, float3 centre, float radius) {
    return distance(eye, centre) - radius;
}

// This function creates a ray with a specified origin and direction.
Ray CreateRay(float3 origin, float3 direction) {
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    return ray;
}

// This function creates a ray in world space originating from the camera
Ray CreateCameraRay(float2 uv) {
    //Calculate origin (start position)
    float3 origin = mul(_CameraToWorld, float4(0,0,0,1)).xyz;
    //Calculate the ray direction
    float3 direction = mul(_CameraInverseProjection, float4(uv,0,1)).xyz;
    direction = mul(_CameraToWorld, float4(direction,0)).xyz;
    direction = normalize(direction);
    return CreateRay(origin,direction);
}

// polynomial smooth min (k = 0.1);
// from https://www.iquilezles.org/www/articles/smin/smin.htm
float4 Blend( float a, float b, float3 colA, float3 colB, float k )
{
    float h = clamp( 0.5+0.5*(b-a)/k, 0.0, 1.0 );
    float blendDst = lerp( b, a, h ) - k*h*(1.0-h);
    float3 blendCol = lerp(colB,colA,h);
    return float4(blendCol, blendDst);
}

float4 Combine(float dstA, float dstB, float3 colourA, float3 colourB) {
    float dst = dstA;
    float3 colour = colourA;
    float4 blend = Blend(dstA,dstB,colourA,colourB, blendStrength);
    dst = blend.w;
    colour = blend.xyz;
    return float4(colour,dst);
}

float GetShapeDistance(Sphere particle, float3 eye) {
   
    return SphereDistance(eye, particle.position, sphereRadius);
    return maxDst;
}

// This function calculates the distance and color of the scene
float4 SceneInfo(float3 eye) {
    float globalDst = maxDst;
    float3 globalColour = waterColor;
    
    // Iterate through all particles (spheres) in the scene.
    for (int i = 0; i < numParticles; i ++) {
        Sphere particle = spheres[i];

        float localDst = GetShapeDistance(particle,eye);
        float3 localColour = waterColor;


        float4 globalCombined = Combine(globalDst, localDst, globalColour, localColour);
        globalColour = globalCombined.xyz;
        globalDst = globalCombined.w;        
    }

    // Return the final color and distance as a float4 vector.
    return float4(globalColour, globalDst);
}

float3 EstimateNormal(float3 p) {
    float x = SceneInfo(float3(p.x+epsilon,p.y,p.z)).w - SceneInfo(float3(p.x-epsilon,p.y,p.z)).w;
    float y = SceneInfo(float3(p.x,p.y+epsilon,p.z)).w - SceneInfo(float3(p.x,p.y-epsilon,p.z)).w;
    float z = SceneInfo(float3(p.x,p.y,p.z+epsilon)).w - SceneInfo(float3(p.x,p.y,p.z-epsilon)).w;
    return normalize(float3(x,y,z));
}

//Calculates the shadow intensity based on raymarching through the scene.
float CalculateShadow(Ray ray, float dstToShadePoint) {
    float rayDst = 0;
    int marchSteps = 0;
    float shadowIntensity = .2;
    float brightness = 1;

    // March along the ray until reaching the shade point.
    while (rayDst < dstToShadePoint) {
        marchSteps ++;
        
        // Get scene information (color and distance) at the current ray origin.
        float4 sceneInfo = SceneInfo(ray.origin);
        float dst = sceneInfo.w;
        
        if (dst <= epsilon) {
            return shadowIntensity;
        }

        brightness = min(brightness,dst*200);

        ray.origin += ray.direction * dst;
        rayDst += dst;
    }

    // Calculate the final shadow intensity based on the shadow intensity and brightness.
    return shadowIntensity + (1-shadowIntensity) * brightness;
}

float LinearEyeDepth( float rawdepth )
{
    float _NearClip = 0.3;
    float FarClip = 1000;
    float x, y, z, w;
    #if SHADER_API_GLES3 // insted of UNITY_REVERSED_Z
        x = -1.0 + _NearClip/ FarClip;
        y = 1;
        z = x / _NearClip;
        w = 1 / _NearClip;
    #else
        x = 1.0 - _NearClip/ FarClip;
        y = _NearClip / FarClip;
        z = x / _NearClip;
        w = y / _NearClip;
    #endif
    
    return 1.0 / (z * rawdepth + w);
}

// Entry point for the compute shader.
[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    uint width,height;
    Destination.GetDimensions(width, height);

    Destination[id.xy] = Source[id.xy];

    float2 uv = id.xy / float2(width,height) * 2 - 1;
    float rayDst = 0;

    Ray ray = CreateCameraRay(uv);
    int marchSteps = 0;

    float depth = LinearEyeDepth(_DepthTexture[id.xy]);

    // Ray marching loop until the maximum distance is reached.
    while (rayDst < maxDst) {
        marchSteps ++;

        // Get scene information (color and distance) from the ray origin.
        float4 sceneInfo = SceneInfo(ray.origin);
        float dst = sceneInfo.w;

        // Early termination: if ray's distance is beyond the depth value
        if (rayDst >= depth) {
            Destination[id.xy] = Source[id.xy];
            break;
        }
        
        if (dst <= epsilon) {
            // Calculate shading parameters.
            float3 pointOnSurface = ray.origin + ray.direction * dst;
            float3 normal = EstimateNormal(pointOnSurface - ray.direction * epsilon);
            float3 lightDir = -_Light;
            float lighting = saturate(saturate(dot(normal,lightDir))) ;
            float3 reflectDir = reflect(-lightDir, normal);
            float spec = pow(max(dot(ray.direction, reflectDir), 0.0), 32);
            float3 specular = 0.7 * spec * float3(1,1,1);

            // Retrieve scene color and apply shading.
            float3 col = sceneInfo.xyz;
            float3 t1 = cross(normal, float3(0,0,1));
            float3 t2 = cross(normal, float3(0,1,0));
            float3 tangent = float3(0,0,0);
            if (length(t1) > length(t2)) {
                tangent = normalize(t1);
            }
            else {
                tangent = normalize(t2);
            }
            float3x3 tangentMatrix = float3x3(tangent,cross(tangent, normal),normal);
            float3 viewDir = normalize(pointOnSurface-_CameraPos);
            float3 refracted = mul(tangentMatrix, refract(viewDir, normal,1));
            
            // Apply shading and assign the result to the destination texture.
            Destination[id.xy] = float4(lerp(col, Source[id.xy+(refracted.xy)], 0.8) * (specular + _AmbientLight + lighting * 0.01),1);
           
            //Exit after shading
            break;  
        }

        ray.origin += ray.direction * dst;
        rayDst += dst;
    }
}